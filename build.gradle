apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/src/main/groovy/CodeNarc.gradle'

group='org.svenehrke'
version = '0.0.2'
archivesBaseName = 'dt_java'

idea {
	project {
		jdkName = '1.6'
	}
	module {
		downloadSources = false
		downloadJavadoc = false
	}
}

repositories {
	mavenRepo urls: "${userDeploymentRepoBaseURL.replace('/localhost', '')}/releases"
	mavenCentral()
}


dependencies {
	compile 'org.svenehrke:directory_template:0.0.3'
	groovy localGroovy()
}


task zipSimpleJava(type: Zip) {
	from 'templatedirectory/simplejava'
	baseName = 'dt_simplejava'
	version = ''
}

task zipSimpleSwingJava(type: Zip) {
	from 'templatedirectory/simpleswing'
	baseName = 'dt_simpleswing'
	version = ''
}

task zipGradle(type: Zip) {
	from 'templatedirectory'
	baseName = 'dt_gradle'
	version = ''
	include 'gradle/**'
}

jar {
	dependsOn zipSimpleJava, zipSimpleSwingJava, zipGradle
	from "$buildDir/distributions/dt_simplejava.zip"
	from "$buildDir/distributions/dt_simpleswing.zip"
	from "$buildDir/distributions/dt_gradle.zip"
}

// see http://evgeny-goldin.com/wiki/Gradle (section 'Setting the repository URL at the time the DAG is ready')
gradle.taskGraph.whenReady { tg ->
	if (!tg.hasTask(':makeRelease')) {
		version += '-SNAPSHOT'
		uploadArchives.repositories['mavenDeployer'].repository.url = "${userDeploymentRepoBaseURL}/snapshots"
	}
}

uploadArchives {
	repositories.mavenDeployer {
		repository(url: "${userDeploymentRepoBaseURL}/releases")
	}
}

task makeSnapshot(dependsOn: 'uploadArchives')

task makeRelease(dependsOn: 'uploadArchives')

task wrapper(type: Wrapper) {
	gradleVersion = '1.0-milestone-5'
}

